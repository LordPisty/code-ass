/**
 *  Tasks:
 *  clean - delete all build artifacts
 *  test - run all unit tests (reports -> 'build/reports/tests/', results -> 'build/test-results/')
 *  intTest - run integration tests (reports -> 'build/reports/tests/', results -> 'build/test-results/')
 *  generateJavadocs - generate javadoc documentation into 'build/docs/javadoc/'
 *  build - build application
 *  run - run main method, expects sorting criteria and direction parameters: e.g. -PappArgs="['rating', 'descending']"
 */

apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 1.8
version = '1.0'
mainClassName = "com.hotels.Main"

repositories {
    mavenCentral()
}

ext {
    jacksonVersion = "2.6.2"
}

sourceSets {
    integrationTest {
        java {
            srcDir 'src/integration-test/java'
        }
        resources {
            srcDir 'src/integration-test/resources'
        }
        compileClasspath += sourceSets.main.compileClasspath
        compileClasspath += sourceSets.main.runtimeClasspath
        compileClasspath += sourceSets.test.runtimeClasspath
    }
}

dependencies {

    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'

    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.6'

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

}

run {
    if ( project.hasProperty("appArgs") ) {
        args Eval.me(appArgs)
    }
}

task generateJavadocs(type: Javadoc) {
    source = sourceSets.main.allJava
}

task intTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath += sourceSets.integrationTest.runtimeClasspath
}